plugins {
    id 'java'
    id 'application'
}

group = 'com.thojensen'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    maven {
        url 'https://repo.eclipse.org/content/groups/releases/'
    }
}

ext {
    swtVersion = '3.122.0'
}

def osgiPlatform = {
    def os = System.getProperty('os.name').toLowerCase()
    def arch = System.getProperty('os.arch').toLowerCase()

    if (os.contains('win')) {
        return 'win32.win32.x86_64'
    } else if (os.contains('mac')) {
        return arch.contains('aarch64') ? 'cocoa.macosx.aarch64' : 'cocoa.macosx.x86_64'
    } else if (os.contains('linux')) {
        return 'gtk.linux.x86_64'
    } else {
        throw new GradleException("Unsupported operating system or architecture")
    }
}()

configurations.all {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        if (details.requested.group == 'org.eclipse.platform' && details.requested.name.startsWith('org.eclipse.swt')) {
            details.useTarget group: details.requested.group, name: "org.eclipse.swt.${osgiPlatform}", version: details.requested.version
        }
    }
}

dependencies {
    implementation "org.eclipse.platform:org.eclipse.swt:${swtVersion}"
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
}

application {
    mainClass = 'com.thojensen.Calculator'
}

run {
    if (System.getProperty('os.name').toLowerCase().contains('mac')) {
        jvmArgs = ['-XstartOnFirstThread']
    }
}

test {
    useJUnitPlatform()
}